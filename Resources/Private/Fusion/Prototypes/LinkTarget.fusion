prototype(Button:LinkTarget) < prototype(Fusion:Value) {
	property = ${property ? property : 'link'}
	target = ${Configuration.setting('Dotpulse.Button.externalTarget')}
	assetExternal = ${String.toLowerCase(Configuration.setting('Dotpulse.Button.assetExternal'))}

	@context {
		read = ${q(node).property(this.property)}
		target = ${this.target}
		assetExternal = ${this.assetExternal}
	}

	value = Fusion:Case {
		@context.linkType = Button:LinkType {
			read = ${read}
		}
		isAsset {
			condition = ${linkType == 'asset'}
			renderer = Fusion:Value {
				mediatype = ${String.toLowerCase(Neos.Link.convertUriToObject(read).resource.mediatype)}
				regex = ${'(' + String.replace(assetExternal, ',', '|') + ')'}
				value = ${String.pregMatch(this.mediatype, this.regex)[0] ? target : false}
			}
		}
		isExternal {
			condition = ${linkType == 'external'}
			renderer = ${target}
		}
		default {
			condition = true
			renderer = false
		}
	}
}
